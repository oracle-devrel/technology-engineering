data "oci_artifacts_container_configuration" "ocir_config" {
  compartment_id = var.compartment_id
}

data "oci_identity_region_subscriptions" "oci_region_subscriptions" {
  tenancy_id = var.tenancy_id
}

locals {
  region_key = lower([for s in data.oci_identity_region_subscriptions.oci_region_subscriptions.region_subscriptions : s if s.region_name == var.region][0].region_key)
  namespace = data.oci_artifacts_container_configuration.ocir_config.namespace
}


resource oci_devops_deploy_artifact argo_chart {
  argument_substitution_mode = "NONE"
  deploy_artifact_source {
    chart_url = "oci://${local.region_key}.ocir.io/${local.namespace}/${var.ocir_repo_path_prefix}/argo-cd"
    deploy_artifact_source_type = "HELM_CHART"
    deploy_artifact_version     = "$${version}"
    helm_verification_key_source {
      verification_key_source_type = "NONE"
    }
  }
  deploy_artifact_type = "HELM_CHART"
  description          = "Location to the internal argo-cd chart generated by the build pipeline"
  display_name         = "argo-cd-chart"
  project_id = oci_devops_project.devops_project.id
}

resource oci_devops_deploy_artifact argo_chart_values {
  argument_substitution_mode = "SUBSTITUTE_PLACEHOLDERS"
  deploy_artifact_source {
    base64encoded_content = filebase64("${path.root}/templates/argo-values.yml")
    deploy_artifact_source_type = "INLINE"
  }
  deploy_artifact_type = "GENERIC_FILE"
  description          = "Values of the argo-cd Helm Charts"
  display_name         = "argo-cd-chart-values"
  project_id = oci_devops_project.devops_project.id
}
